buildscript {
    ext {
        versions = [
                'android': [
                    'gradle_plugin': '3.2.1',
                    'x': [
                            'appcompat': '1.0.0',
                            'lifecycle': '2.0.0'
                    ]
                ],
                'concurrent_unit': '0.4.3',
                'junit' : '4.12',
                'kotlin': [
                        'coroutines': '1.0.0',
                        'lang': '1.3.0'
                ],
                'mockito_kotlin': '2.0.0',
                'publish': [
                        'dokka': '0.9.17',
                        'maven_plugin': '2.1',
                        'bintray_plugin': '1.8.4'
                ],
                'static_analysis': [
                        'ktlint': '0.29.0',
                ]
        ]

        coreVersions = [
                'targetSdkVersion': 28,
                'minSdkVersion': 15,
                'compileSdkVersion': 28,
                'versionCode': 1,
                'versionName': '0.0.2'
        ]

        depends = [
                'android': [
                    'gradle_plugin': "com.android.tools.build:gradle:${versions.android.gradle_plugin}",
                    'x': [
                            'appcompat': "androidx.appcompat:appcompat:${versions.android.x.appcompat}",
                            'lifecycle': [
                                    'livedata': "androidx.lifecycle:lifecycle-livedata:${versions.android.x.lifecycle}",
                                    'runtime': "androidx.lifecycle:lifecycle-runtime:${versions.android.x.lifecycle}",
                                    'testing': "androidx.arch.core:core-testing:${versions.android.x.lifecycle}"
                            ]
                    ]
                ],
                'concurrent_unit': "net.jodah:concurrentunit:${versions.concurrent_unit}",
                'junit' : "junit:junit:${versions.junit}",
                'kotlin': [
                        'coroutines'   : "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.kotlin.coroutines}",
                        'gradle_plugin': "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin.lang}",
                        'stdlib'       : "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin.lang}"
                ],
                'mockito_kotlin': "com.nhaarman.mockitokotlin2:mockito-kotlin:${versions.mockito_kotlin}",
                'publish': [
                        'dokka': "org.jetbrains.dokka:dokka-android-gradle-plugin:${versions.publish.dokka}",
                        'maven_plugin': "com.github.dcendents:android-maven-gradle-plugin:${versions.publish.maven_plugin}",
                        'bintray_plugin': "com.jfrog.bintray.gradle:gradle-bintray-plugin:${versions.publish.bintray_plugin}"
                ],
                'static_analysis': [
                        'ktlint': "com.github.shyiko:ktlint:${versions.static_analysis.ktlint}",
                ]
        ]
    }

    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath depends.kotlin.gradle_plugin
        classpath depends.android.gradle_plugin
        classpath depends.publish.maven_plugin
        classpath depends.publish.bintray_plugin
        classpath depends.publish.dokka
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
}

allprojects {
    tasks.withType(Test) {
        testLogging {
            exceptionFormat "full"
            showCauses true
            showExceptions true
            showStackTraces true
            showStandardStreams true
            events = ["passed", "skipped", "failed", "standardOut", "standardError"]
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions.allWarningsAsErrors = true
}
